prototype(Carbon.ResponsiveImage:Internal.Attributes.Lightbox) < prototype(Neos.Fusion:Component) {
    asset = false
    config = ${Configuration.setting('Carbon.ResponsiveImage.lightbox')}

    id = null
    class = null
    caption = null
    title = null
    style = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        dimensions = afx`<Carbon.ResponsiveImage:Internal.GetDimensions asset={props.asset} {...props.config} />`
        renderer = Neos.Fusion:Attributes {
            class = Neos.Fusion:DataStructure {
                class = Carbon.ResponsiveImage:Internal.GeneralizeClass {
                    class = ${props.class}
                }
                config = Carbon.ResponsiveImage:Internal.GeneralizeClass {
                    class = ${props.config.class}
                }
                @process {
                    filter = ${Carbon.Array.filter(value)}
                    removeEmpty = ${value ? value : false}
                }
            }
            id = ${props.id}
            target = ${props.config.target}
            rel = ${props.config.rel}
            data-title = ${props.caption}
            data-size = ${props.dimensions ? props.dimensions.combined : false}
            title = ${props.title}
            style = ${props.style}
            href = afx`<Neos.Neos:ImageUri asset={props.asset} {...props.config} />`
        }
    }
}
